load("@build_stack_rules_hugo//hugo:rules.bzl", "hugo_site", "hugo_serve")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load", "oci_push")

hugo_site(
    name = "site",
    config = "config.toml", 
    content = glob(["content/**"]),
    layouts = glob(["layouts/**"]),
    static = glob(["static/**"]),
    theme = "//themes/espouse",
    quiet = False,
)

hugo_serve(
    name = "serve",
    dep = [":site"],
    )

pkg_tar(
    name = "static_assets",
    srcs = [":site"],
    package_dir = "/usr/share/nginx/html",
)

oci_image(
    name = "nginx_container",
    base = "@distroless_base",
    entrypoint = ["/nginx", "-g", "daemon off;"],
    tars = [
        ":static_assets",
        ":nginx_configs",
    ],
    visibility = ["//visibility:public"],
)

# Load target for local testing
oci_load(
    name = "nginx_load",
    image = ":nginx_container",
    repo_tags = ["ghcr.io/tstapler/personal-website:latest"],
)

# Filegroup to access the tarball output
filegroup(
    name = "nginx_image_tarball",
    srcs = [":nginx_load"],
    output_group = "tarball",
)

# Runnable target to test locally
genrule(
    name = "nginx_test",
    srcs = [":nginx_image_tarball"],
    outs = ["run_nginx.sh"],
    cmd = """echo '#!/bin/bash
docker load -i $(location :nginx_image_tarball)
docker run -p 1314:80 ghcr.io/tstapler/personal-website:latest' > $@
    """,
    executable = True,
)

# Push target for CI
genrule(
    name = "docker_push",
    srcs = [":nginx_image_tarball"],
    outs = ["push.sh"],
    cmd = """echo '#!/bin/bash
docker load -i $(location :nginx_image_tarball)
docker push ghcr.io/tstapler/personal-website:latest' > $@
    """,
    executable = True,
)

pkg_tar(
    name = "nginx_configs",
    srcs = glob(["deployment/personal-website/files/**"]),
    package_dir = "/etc/nginx",
)

